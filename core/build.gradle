plugins {
    id 'net.ltgt.apt' version '0.10'
}

apply plugin: 'java'

/////////////////////////////////////////////////////////////////////////
// for AutoValue in ASide
/////////////////////////////////////////////////////////////////////////
apply plugin: 'idea'
idea {
    module {
        generatedSourceDirs += project.file('build/generated/source/apt')
    }
}
/////////////////////////////////////////////////////////////////////////

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile libraries.gson
    compile libraries.findbugsAnnotations
    compile libraries.rxJava

    compileOnly libraries.autoValue
    apt libraries.autoValue

    testCompile libraries.jUnit
    testCompile libraries.mockitoCore
    testCompile libraries.festAssertCore
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task createCoreTestFixturesJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output.classesDir
    classifier = 'test'
    include '**/*Fixtures.class'
}

configurations {
    coreTestFixtures.extendsFrom testCompile
}

artifacts {
    coreTestFixtures createCoreTestFixturesJar
}
