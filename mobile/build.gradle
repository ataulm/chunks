apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    fabric {
        file project.file('../fabric.properties')
    }
    signing {
        file project.file('../signing.properties')
    }
}

String MISSING_FABRIC_KEY = 'missing_fabric_key'
String fabricApiKey = buildProperties.fabric['apiKey'].or(MISSING_FABRIC_KEY).string
boolean shouldEnableFabric = fabricApiKey != MISSING_FABRIC_KEY
if (shouldEnableFabric) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion versions.androidSdk.compile

    defaultConfig {
        applicationId 'com.ataulm.chunks'
        minSdkVersion versions.androidSdk.min
        targetSdkVersion versions.androidSdk.target
        versionCode 8
        versionName '1.1.2'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigBoolean 'SHOULD_ENABLE_FABRIC', shouldEnableFabric
        manifestPlaceholders = [
                fabricApiKey: fabricApiKey
        ]
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signing['storeFile'].or('/absolute/path/to/keystore/file').string)
            storePassword buildProperties.signing['storePassword'].or('password').string
            keyAlias buildProperties.signing['keyAlias'].or('keyalias').string
            keyPassword buildProperties.signing['keyPassword'].or('keypassword').string
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("../lint-config.xml")
    }
}

dependencies {
    compile project(':core')

    compile libraries.accessibilitools
    compile libraries.androidxAppCompat
    compile libraries.androidxLegacySupportV13
    compile libraries.androidxMaterial
    compile libraries.androidxRecyclerView
    compile libraries.androidxRoomRuntime
    compile libraries.butterknife
    compile(libraries.crashlytics) { transitive = true }
    compile libraries.landingStrip
    compile libraries.rxAndroid
    compile libraries.viewPagerAdapter

    provided libraries.autoValue
    annotationProcessor libraries.androidxRoomCompiler
    annotationProcessor libraries.autoValue
    annotationProcessor libraries.butterknifeCompiler

    androidTestCompile project(path: ':core', configuration: 'coreTestFixtures')
    androidTestCompile libraries.dexmaker
    androidTestCompile libraries.dexmakerMockito
    androidTestCompile libraries.espressoSupport
    debugCompile libraries.espressoSupportExtras
    androidTestCompile libraries.mockitoCore
    androidTestCompile libraries.androidxEspressoCore
    androidTestCompile libraries.androidxTestExtJunit
    androidTestCompile libraries.androidxTestRules
}
