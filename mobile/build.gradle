apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'
apply plugin: 'io.fabric'

buildProperties {
    fabric {
        file project.file('../fabric.properties')
    }
    signing {
        file project.file('../signing.properties')
    }
}

android {
    compileSdkVersion versions.androidSdk.compile
    buildToolsVersion versions.androidSdk.buildTools

    defaultConfig {
        applicationId 'com.ataulm.chunks'
        minSdkVersion versions.androidSdk.min
        targetSdkVersion versions.androidSdk.target
        versionCode 5
        versionName '1.0.5'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [
                fabricApiKey: buildProperties.fabric['apiKey'].string
        ]
    }

    signingConfigs {
        release {
            storeFile file(buildProperties.signing['storeFile'].string)
            storePassword buildProperties.signing['storePassword'].string
            keyAlias buildProperties.signing['keyAlias'].string
            keyPassword buildProperties.signing['keyPassword'].string
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("../lint-config.xml")
    }
}

dependencies {
    compile project(":core")

    compile libraries.accessibilitools
    annotationProcessor libraries.autoValue
    provided libraries.autoValue
    compile libraries.butterknife
    annotationProcessor libraries.butterknifeCompiler
    compile(libraries.crashlytics) { transitive = true }
    compile libraries.landingStrip
    compile libraries.rxAndroid
    compile libraries.supportAppCompatV7
    compile libraries.supportV13
    compile libraries.supportRecyclerview
    compile libraries.supportDesign
    compile libraries.viewPagerAdapter

    debugCompile libraries.espressoSupportExtras
    androidTestCompile(libraries.espressoSupport) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile libraries.mockitoCore
    androidTestCompile libraries.dexmaker
    androidTestCompile libraries.dexmakerMockito
    androidTestCompile project(path: ':core', configuration: 'coreTestFixtures')

    // Android runner and rules support
    androidTestCompile(libraries.androidTestRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    // Espresso support
    androidTestCompile(libraries.androidEspressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
